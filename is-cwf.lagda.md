
```
-- I am relying on type-constructor injectivity for proving dependent UIP
-- I don't know whether injectivity of '_â‰¡_' is provable without this
{-# OPTIONS --rewriting --injective-type-constructors #-}

open import Data.Unit
open import Relation.Binary.PropositionalEquality hiding ([_])
  renaming (trans to _âˆ™_)

open import cwf-simple renaming (CwF-simple to CwF)
open import subst
open import laws

module is-cwf where

module FirstAttempt where
  -- Here, we get stuck! 

  record _âŠ¢_ (Î“ : Con)(A : Ty) : Set where
    constructor tm
    field
      {qq} : Sort
      xx : Î“ âŠ¢[ qq ] A

  record _âŠ¨_ (Î“ Î” : Con) : Set where
    constructor tms
    field
      {qq} : Sort
      xss : Î“ âŠ¨[ qq ] Î”

  variable
    xx yy zz : Î“ âŠ¢ A 
    xss yss zss : Î“ âŠ¨ Î” 

  stlc : CwF
  stlc .CwF.Con = Con
  stlc .CwF.Ty = Ty
  stlc .CwF._âŠ¢_ = _âŠ¢_
  stlc .CwF._âŠ¨_ = _âŠ¨_
  stlc .CwF.id = tms id
  stlc .CwF._âˆ˜_ (tms xs) (tms ys) = tms (xs âˆ˜ ys)
  stlc .CwF.idâˆ˜ = cong tms idâˆ˜
  stlc .CwF.âˆ˜id = cong tms âˆ˜id
  stlc .CwF.âˆ˜âˆ˜ = cong tms (sym âˆ˜âˆ˜)
  stlc .CwF._[_] (tm x) (tms xs) = tm (x [ xs ])
  stlc .CwF.[id] = cong tm [id]
  stlc .CwF.[âˆ˜] {t = tm x} = cong tm (sym ([âˆ˜] {x = x}))
  stlc .CwF.â€¢ = â€¢
  stlc .CwF.Îµ = tms {qq = V} Îµ
  -- We are stuck!
  stlc .CwF.â€¢-Î· {Î´ = tms Îµ} = {!!}
  stlc .CwF._â–·_ = _â–·_
  -- Here, we need to coerce 'xs' and 'x' to 'Sort' 'q âŠ” r'. 
  -- Do-able, but a bit painful...
  stlc .CwF._,_ (tms {qq = q} xs) (tm {qq = r} x) 
    = tms {qq = q âŠ” r} ({!xs!} , tmâŠ‘ (âŠ‘âŠ”r {q = q}) x)
  stlc .CwF.Ï€â‚€ (tms (xs , x)) = tms xs
  stlc .CwF.Ï€â‚ (tms (xs , x)) = tm x
  stlc .CwF.â–·-Î²â‚€ = {!!}
  stlc .CwF.â–·-Î²â‚ = {!!}
  stlc .CwF.â–·-Î· = {!!}
  stlc .CwF.Ï€â‚€âˆ˜ {Î¸ = tms (xs , x)} = refl
  stlc .CwF.Ï€â‚âˆ˜ {Î¸ = tms (xs , x)} = refl
  stlc .CwF.o = o
  stlc .CwF._â‡’_ = _â‡’_
  stlc .CwF._Â·_ (tm x) (tm y) = tm {qq = T} (tmâŠ‘ âŠ‘t x Â· tmâŠ‘  âŠ‘t y)
  stlc .CwF.Æ›_ (tm x) = tm {qq = T} (Æ› tmâŠ‘ âŠ‘t x)
  stlc .CwF.Â·[] = {!!}
  stlc .CwF.Æ›[] = {!!}

module SecondAttempt where

  tm*âŠ‘ : q âŠ‘ s â†’ Î“ âŠ¨[ q ] Î” â†’ Î“ âŠ¨[ s ] Î”
  tm*âŠ‘ qâŠ‘s Îµ = Îµ
  tm*âŠ‘ qâŠ‘s (Ïƒ , x) = tm*âŠ‘ qâŠ‘s Ïƒ , tmâŠ‘ qâŠ‘s x

  _âŠ¢_ = _âŠ¢[ T ]_
  _âŠ¨_ = _âŠ¨[ T ]_

  idT : Î“ âŠ¨ Î“
  idT = tm*âŠ‘ vâŠ‘t id

  -- Turning these into rewrites might be a good idea...
  âŠ‘âˆ˜ : tm*âŠ‘ vâŠ‘t xs âˆ˜ ys â‰¡ xs âˆ˜ ys
  âŠ‘âˆ˜ {xs = Îµ} = refl
  âŠ‘âˆ˜ {xs = xs , x} = congâ‚‚ _,_ âŠ‘âˆ˜ refl

  suc[idâº] : x [ id âº A ] â‰¡ suc x A
  suc[idâº] {x = x} {A = A} =
    x [ id âº A ]
    â‰¡âŸ¨ âº-nat[]v {i = x} âŸ© 
    suc (x [ id ]) A
    â‰¡âŸ¨ cong (Î» y â†’ suc y A) [id] âŸ©
    suc x A âˆ

  âŠ‘suc : tmâŠ‘ âŠ‘t (suc[ q ] x A) â‰¡ tmâŠ‘ âŠ‘t x [ id âº A ]
  âŠ‘suc {q = V} {x = x} {A = A} = cong (`_) (sym suc[idâº])
  âŠ‘suc {q = T} = refl

  âŠ‘âº : tm*âŠ‘ (âŠ‘t {s = q}) (xs âº A) â‰¡ tm*âŠ‘ âŠ‘t xs âº A
  âŠ‘âº {xs = Îµ} = refl
  âŠ‘âº {q = q} {xs = xs , x} {A = A} = congâ‚‚ _,_ âŠ‘âº (âŠ‘suc {x = x})

  âŠ‘zero : tmâŠ‘ âŠ‘t zero[ q ] â‰¡ ` zero {Î“ = Î“} {A = A}
  âŠ‘zero {q = V} = refl
  âŠ‘zero {q = T} = refl

  âŠ‘^ : tm*âŠ‘ (âŠ‘t {s = q}) (xs ^ A) â‰¡ tm*âŠ‘ âŠ‘t xs ^ A
  âŠ‘^ {q = q} = congâ‚‚ _,_ âŠ‘âº (âŠ‘zero {q = q})

  `[âŠ‘] : âˆ€ {x : Î“ âŠ¢[ V ] A} {ys : Î” âŠ¨[ q ] Î“} 
       â†’ x [ tm*âŠ‘ âŠ‘t ys ] â‰¡ tmâŠ‘ âŠ‘t x [ ys ]
  `[âŠ‘] {x = zero} {ys = ys , y} = refl
  `[âŠ‘] {x = suc x _} {ys = ys , y} = `[âŠ‘] {x = x}

  [âŠ‘] : âˆ€ {x : Î“ âŠ¢[ T ] A} {ys : Î” âŠ¨[ q ] Î“} â†’ x [ tm*âŠ‘ âŠ‘t ys ] â‰¡ x [ ys ]
  [âŠ‘] {x = ` x} = `[âŠ‘] {x = x}
  [âŠ‘] {x = x Â· y} = congâ‚‚ _Â·_ ([âŠ‘] {x = x}) ([âŠ‘] {x = y})
  [âŠ‘] {x = Æ› x} {ys = ys} = 
    Æ› x [ tm*âŠ‘ âŠ‘t ys ^ _ ]
    â‰¡âŸ¨ cong (Î» Ï â†’ Æ› x [ Ï ]) (sym âŠ‘^) âŸ©
    Æ› x [ tm*âŠ‘ âŠ‘t (ys ^ _) ]
    â‰¡âŸ¨ cong Æ›_ ([âŠ‘] {x = x}) âŸ© 
    Æ› x [ ys ^ _ ] âˆ

  âˆ˜âŠ‘ : âˆ€ {xs : Î” âŠ¨[ T ] Î“} {ys : Î˜ âŠ¨[ q ] Î”} â†’ xs âˆ˜ tm*âŠ‘ âŠ‘t ys â‰¡ xs âˆ˜ ys
  âˆ˜âŠ‘ {xs = Îµ} {ys = ys} = refl
  âˆ˜âŠ‘ {xs = xs , x} = congâ‚‚ _,_ âˆ˜âŠ‘ ([âŠ‘] {x = x})
  
  âˆ˜idâº : {xs : Î” âŠ¨[ q ] Î“} â†’ xs âº A â‰¡ xs âˆ˜ id âº A 
  âˆ˜idâº {A = A} {xs = xs} =
    xs âº A
    â‰¡âŸ¨ cong (_âº A) (sym âˆ˜id) âŸ©
    (xs âˆ˜ id) âº A
    â‰¡âŸ¨ sym (âº-natâˆ˜ {xs = xs} {ys = id}) âŸ©
    xs âˆ˜ id âº A âˆ

  Ï€â‚€ : Î” âŠ¨[ q ] (Î“ â–· A) â†’ Î” âŠ¨[ q ] Î“
  Ï€â‚€ (Î´ , M) = Î´

  Ï€â‚ : Î” âŠ¨[ q ] (Î“ â–· A) â†’ Î” âŠ¢[ q ] A
  Ï€â‚ (Î´ , M) = M

  stlc : CwF
  stlc .CwF.Con = Con
  stlc .CwF.Ty = Ty
  stlc .CwF._âŠ¢_ = _âŠ¢_
  stlc .CwF._âŠ¨_ = _âŠ¨_
  stlc .CwF.id = idT
  stlc .CwF._âˆ˜_ = _âˆ˜_
  stlc .CwF.idâˆ˜ = âŠ‘âˆ˜ âˆ™ idâˆ˜
  stlc .CwF.âˆ˜id = âˆ˜âŠ‘ âˆ™ âˆ˜id
  stlc .CwF.âˆ˜âˆ˜ = sym âˆ˜âˆ˜
  stlc .CwF._[_] = _[_]
  stlc .CwF.[id] {t = x} = ([âŠ‘] {x = x}) âˆ™ [id]
  stlc .CwF.[âˆ˜] {t = x} = sym ([âˆ˜] {x = x})
  stlc .CwF.â€¢ = â€¢
  stlc .CwF.Îµ = Îµ
  stlc .CwF.â€¢-Î· {Î´ = Îµ} = refl
  stlc .CwF._â–·_ = _â–·_
  stlc .CwF._,_ = _,_
  stlc .CwF.Ï€â‚€ = Ï€â‚€
  stlc .CwF.Ï€â‚ = Ï€â‚
  stlc .CwF.â–·-Î²â‚€ = refl
  stlc .CwF.â–·-Î²â‚ = refl
  stlc .CwF.â–·-Î· {Î´ = xs , x} = refl
  stlc .CwF.Ï€â‚€âˆ˜ {Î¸ = xs , x} = refl
  stlc .CwF.Ï€â‚âˆ˜ {Î¸ = xs , x} = refl
  stlc .CwF.o = o
  stlc .CwF._â‡’_ = _â‡’_
  stlc .CwF._Â·_ = _Â·_
  stlc .CwF.Æ›_ = Æ›_
  stlc .CwF.Â·[] = refl
  stlc .CwF.Æ›[] {A = A} {t = x} {Î´ = ys} = 
    Æ› x [ ys ^ A ]
    â‰¡âŸ¨ cong (Î» Ï â†’ Æ› x [ Ï , ` zero ]) (âˆ˜idâº {A = A} {xs = ys}) âŸ© 
    Æ› x [ ys âˆ˜ id âº A , ` zero ]
    â‰¡âŸ¨ cong (Î» Ï â†’ Æ› x [ Ï , ` zero ]) (sym (âˆ˜âŠ‘ {xs = ys} {ys = id âº A})) âŸ©
    Æ› x [ ys âˆ˜ tm*âŠ‘ vâŠ‘t (id âº A) , ` zero ] âˆ
  
  -- Conversion to and from the initial CwF
  
  open import initial-cwf as ICwF 
    using (_â‰¡[_]â‰¡_; rec-con; rec-ty; rec-tm; rec-tms
          ; elim-con; elim-ty; elim-tm; elim-tms)
  open ICwF.Motive
  open ICwF.Cases

  Conâ‰¡ : rec-con stlc Î“ â‰¡ Î“
  Tyâ‰¡  : rec-ty stlc A â‰¡ A

  Conâ‰¡ {Î“ = â€¢} = refl
  Conâ‰¡ {Î“ = Î“ â–· A} = congâ‚‚ _â–·_ Conâ‰¡ Tyâ‰¡

  Tyâ‰¡ {A = o} = refl
  Tyâ‰¡ {A = A â‡’ B} = congâ‚‚ _â‡’_ Tyâ‰¡ Tyâ‰¡

  {-# REWRITE Conâ‰¡ Tyâ‰¡ #-}

  to-stlc-tm : Î“ ICwF.âŠ¢ A â†’ Î“ âŠ¢ A
  to-stlc-tm = rec-tm stlc

  to-stlc-tms : Î” ICwF.âŠ¨ Î“ â†’ Î” âŠ¨ Î“
  to-stlc-tms = rec-tms stlc

  to-cwf-tm : Î“ âŠ¢[ q ] A â†’ Î“ ICwF.âŠ¢ A
  to-cwf-tm zero = ICwF.vz
  to-cwf-tm (suc x _) = ICwF.vs (to-cwf-tm x)
  to-cwf-tm (` x) = to-cwf-tm x
  to-cwf-tm (M Â· N) = to-cwf-tm M ICwF.Â· to-cwf-tm N
  to-cwf-tm (Æ› M) = ICwF.Æ› (to-cwf-tm M)

  to-cwf-tms : Î” âŠ¨[ q ] Î“ â†’ ICwF._âŠ¨_ Î” Î“
  to-cwf-tms Îµ = ICwF.Îµ
  to-cwf-tms (Î´ , M) = to-cwf-tms Î´ ICwF., to-cwf-tm M

  to-stlc-inv-tm : âˆ€ {M : Î“ âŠ¢[ q ] A} â†’ to-stlc-tm (to-cwf-tm M) â‰¡ tmâŠ‘ âŠ‘t M
  to-stlc-inv-tm {M = zero} = refl
  to-stlc-inv-tm {M = suc x B} = 
    to-stlc-tm (to-cwf-tm x) [ tm*âŠ‘ vâŠ‘t (id âº B) ]
    â‰¡âŸ¨ [âŠ‘] {x = to-stlc-tm (to-cwf-tm x)} âŸ©
     (to-stlc-tm (to-cwf-tm x)) [ id âº B ]
    â‰¡âŸ¨ cong (Î» M â†’ suc[ _ ] M B) (to-stlc-inv-tm {M = x}) âŸ©
    ` x [ id âº B ]
    â‰¡âŸ¨ cong `_ suc[idâº] âŸ©
    ` suc x B âˆ
  to-stlc-inv-tm {M = ` x} = to-stlc-inv-tm {M = x}
  to-stlc-inv-tm {M = M Â· N} 
    = congâ‚‚ _Â·_ (to-stlc-inv-tm {M = M}) (to-stlc-inv-tm {M = N})
  to-stlc-inv-tm {M = Æ› M} = cong Æ›_ (to-stlc-inv-tm {M = M})

  to-cwf-Ï€â‚€ : âˆ€ {Î´ : Î” âŠ¨ (Î“ â–· A)} 
            â†’ to-cwf-tms (Ï€â‚€ Î´) â‰¡ ICwF.Ï€â‚€ (to-cwf-tms Î´)
  to-cwf-Ï€â‚€ {Î´ = Î´ , M} = sym ICwF.â–·-Î²â‚€

  to-cwf-Ï€â‚ : âˆ€ {Î´ : Î” âŠ¨[ q ] (Î“ â–· A)} 
            â†’ to-cwf-tm (Ï€â‚ Î´) â‰¡ ICwF.Ï€â‚ (to-cwf-tms Î´)
  to-cwf-Ï€â‚ {Î´ = Î´ , M} = sym ICwF.â–·-Î²â‚

  to-cwf-âˆ˜ : âˆ€ {Ïƒ : Î˜ âŠ¨ Î”} {Î´ : Î” âŠ¨ Î“}
           â†’ to-cwf-tms (Î´ âˆ˜ Ïƒ) â‰¡ to-cwf-tms Î´ ICwF.âˆ˜ to-cwf-tms Ïƒ 
  to-cwf-[] : âˆ€ {M : Î“ âŠ¢[ q ] A} {Î´ : Î” âŠ¨[ s ] Î“} 
          â†’ to-cwf-tm (M [ Î´ ]) â‰¡ to-cwf-tm M ICwF.[ to-cwf-tms Î´ ]
  to-cwf-^ : âˆ€ {Î´ : Î” âŠ¨[ q ] Î“}
           â†’ to-cwf-tms (Î´ ^ A) â‰¡ to-cwf-tms Î´ ICwF.^ A
  to-cwf-âº : âˆ€ {Î´ : Î” âŠ¨[ q ] Î“}
                â†’ to-cwf-tms (Î´ âº A) 
                â‰¡ to-cwf-tms Î´ ICwF.âˆ˜ ICwF.Ï€â‚€ ICwF.id

  to-cwf-idâ€² : Sort â†’ to-cwf-tms id â‰¡ ICwF.id {Î“ = Î“}

  -- Our classic termination trick
  to-cwf-id : to-cwf-tms id â‰¡ ICwF.id {Î“ = Î“}
  to-cwf-id = to-cwf-idâ€² V
  {-# INLINE to-cwf-id #-}

  to-cwf-vz : to-cwf-tm (zero[_] {Î“ = Î“} {A = A} q) â‰¡ ICwF.Ï€â‚ ICwF.id

  to-cwf-vz {q = V} = refl
  to-cwf-vz {q = T} = refl

  to-cwf-vs : âˆ€ {M : Î“ âŠ¢[ q ] A} {B} 
            â†’ to-cwf-tm (suc[ q ] M B) â‰¡ to-cwf-tm M ICwF.[ ICwF.wk ]
  to-cwf-vs {q = V} = refl
  to-cwf-vs {q = T} {M = M} {B = B} = 
    to-cwf-tm (M [ id âº B ])
    â‰¡âŸ¨ to-cwf-[] {M = M} âŸ©
    to-cwf-tm M ICwF.[ to-cwf-tms (id âº B) ]
    â‰¡âŸ¨ cong (to-cwf-tm M ICwF.[_]) to-cwf-âº âŸ©
    to-cwf-tm M ICwF.[ to-cwf-tms id ICwF.âˆ˜ ICwF.wk ]
    â‰¡âŸ¨ cong (Î» Ï â†’ to-cwf-tm M ICwF.[ Ï ICwF.âˆ˜ ICwF.wk ]) to-cwf-id âŸ©
    to-cwf-tm M ICwF.[ ICwF.id ICwF.âˆ˜ ICwF.wk ]
    â‰¡âŸ¨ cong (to-cwf-tm M ICwF.[_]) ICwF.idâˆ˜  âŸ©
    to-cwf-tm M ICwF.[ ICwF.wk ] âˆ

  to-cwf-^ {q = q} {A = A} {Î´ = Î´} 
    = congâ‚‚ ICwF._,_ to-cwf-âº (to-cwf-vz {q = q})

  to-cwf-tmâŠ‘ : âˆ€ {M : Î“ âŠ¢[ q ] A} â†’ to-cwf-tm (tmâŠ‘ âŠ‘t M) â‰¡ to-cwf-tm M
  to-cwf-tmâŠ‘ {q = V} = refl
  to-cwf-tmâŠ‘ {q = T} = refl

  to-cwf-tm*âŠ‘ : âˆ€ {Î´ : Î” âŠ¨[ q ] Î“} â†’ to-cwf-tms (tm*âŠ‘ âŠ‘t Î´) â‰¡ to-cwf-tms Î´
  to-cwf-tm*âŠ‘ {Î´ = Îµ} = refl
  to-cwf-tm*âŠ‘ {Î´ = Î´ , M} = congâ‚‚ ICwF._,_ to-cwf-tm*âŠ‘ (to-cwf-tmâŠ‘ {M = M})
  
  to-cwf-[] {M = zero} {Î´ = Î´ , N} = sym (ICwF.vz[] {Î´ = to-cwf-tms Î´})
  to-cwf-[] {M = suc M B} {Î´ = Î´ , N} = 
    to-cwf-tm (M [ Î´ ])
    â‰¡âŸ¨ to-cwf-[] {M = M} âŸ©
    to-cwf-tm M ICwF.[ to-cwf-tms Î´ ]
    â‰¡âŸ¨ sym ICwF.vs[] âŸ©
    ICwF.vs (to-cwf-tm M) ICwF.[ to-cwf-tms Î´ ICwF., to-cwf-tm N ] âˆ
  to-cwf-[] {M = ` M} {Î´ = Î´} = 
    to-cwf-tm (tmâŠ‘ âŠ‘t (M [ Î´ ]))
    â‰¡âŸ¨ to-cwf-tmâŠ‘ {M = M [ Î´ ]} âŸ©
    to-cwf-tm (M [ Î´ ])
    â‰¡âŸ¨ to-cwf-[] {M = M} âŸ©
    to-cwf-tm M ICwF.[ to-cwf-tms Î´ ] âˆ
  to-cwf-[] {M = M Â· N} {Î´ = Î´} = 
    to-cwf-tm (M [ Î´ ]) ICwF.Â· to-cwf-tm (N [ Î´ ])
    â‰¡âŸ¨ congâ‚‚ ICwF._Â·_ (to-cwf-[] {M = M}) (to-cwf-[] {M = N}) âŸ©
    to-cwf-tm M ICwF.[ to-cwf-tms Î´ ] ICwF.Â· to-cwf-tm N ICwF.[ to-cwf-tms Î´ ]
    â‰¡âŸ¨ sym ICwF.Â·[] âŸ©
    (to-cwf-tm M ICwF.Â· to-cwf-tm N) ICwF.[ to-cwf-tms Î´ ] âˆ
  to-cwf-[] {M = Æ› M} {Î´ = Î´} =
    ICwF.Æ› to-cwf-tm (M [ Î´ ^ _ ])
    â‰¡âŸ¨ cong ICwF.Æ›_ (to-cwf-[] {M = M} {Î´ = Î´ ^ _}) âŸ©
    ICwF.Æ› to-cwf-tm M ICwF.[ to-cwf-tms (Î´ ^ _) ]
    â‰¡âŸ¨ cong (Î» Ï â†’ ICwF.Æ› (to-cwf-tm M ICwF.[ Ï ])) to-cwf-^ âŸ©
    ICwF.Æ› to-cwf-tm M ICwF.[ to-cwf-tms Î´ ICwF.^ _ ]
    â‰¡âŸ¨ sym ICwF.Æ›[] âŸ©
    (ICwF.Æ› to-cwf-tm M) ICwF.[ to-cwf-tms Î´ ] âˆ
  
  to-cwf-âˆ˜ {Î´ = Îµ} = sym ICwF.â€¢-Î·
  to-cwf-âˆ˜ {Ïƒ = Ïƒ} {Î´ = Î´ , M} = 
    Î´âˆ˜Ïƒ ICwF., M[Ïƒ]
    â‰¡âŸ¨ cong (Î´âˆ˜Ïƒ ICwF.,_) (to-cwf-[] {M = M}) âŸ©
    Î´âˆ˜Ïƒ ICwF., (Mâ€² ICwF.[ Ïƒâ€² ])
    â‰¡âŸ¨ cong (ICwF._, (Mâ€² ICwF.[ Ïƒâ€² ])) to-cwf-âˆ˜ âŸ©
    (Î´â€² ICwF.âˆ˜ Ïƒâ€²) ICwF., (Mâ€² ICwF.[ Ïƒâ€² ])
    â‰¡âŸ¨ sym ICwF.âˆ˜[] âŸ©
    (Î´â€² ICwF., Mâ€²) ICwF.âˆ˜ Ïƒâ€² âˆ
    where
      Î´âˆ˜Ïƒ = to-cwf-tms (Î´ âˆ˜ Ïƒ)
      Î´â€² = to-cwf-tms Î´
      Ïƒâ€² = to-cwf-tms Ïƒ
      Mâ€² = to-cwf-tm M
      M[Ïƒ] = to-cwf-tm (M [ Ïƒ ])

  to-cwf-âº {Î´ = Îµ} = sym ICwF.â€¢-Î·
  to-cwf-âº {A = A} {Î´ = Î´ , M} = 
    to-cwf-tms (Î´ âº A) ICwF., to-cwf-tm (suc[ _ ] M A)
    â‰¡âŸ¨ congâ‚‚ ICwF._,_ to-cwf-âº (to-cwf-vs {M = M}) âŸ©
    (to-cwf-tms Î´ ICwF.âˆ˜ ICwF.Ï€â‚€ ICwF.id) ICwF., (to-cwf-tm M ICwF.[ ICwF.wk ])
    â‰¡âŸ¨ sym ICwF.âˆ˜[] âŸ©
    (to-cwf-tms Î´ ICwF., to-cwf-tm M) ICwF.âˆ˜ ICwF.Ï€â‚€ ICwF.id âˆ

  to-cwf-idâ€² {Î“ = â€¢} _ = sym ICwF.â€¢-Î·
  to-cwf-idâ€² {Î“ = Î“ â–· A} _ = 
    to-cwf-tms (id âº A) ICwF., ICwF.Ï€â‚ ICwF.id
    â‰¡âŸ¨ cong (Î» Ï â†’ Ï ICwF., ICwF.Ï€â‚ ICwF.id) to-cwf-âº âŸ©
    to-cwf-tms id ICwF.^ A
    â‰¡âŸ¨ cong (ICwF._^ A) to-cwf-id âŸ©
    ICwF.id ICwF.^ A
    â‰¡âŸ¨ ICwF.id^ âŸ©
    ICwF.id âˆ

  to-cwf-inv-ğ•„ : ICwF.Motive
  to-cwf-inv-ğ•„ .Coná´± _ = âŠ¤
  to-cwf-inv-ğ•„ .Tyá´±  _ = âŠ¤
  to-cwf-inv-ğ•„ .Tmá´± Î“ A M = to-cwf-tm (to-stlc-tm M) â‰¡ M
  to-cwf-inv-ğ•„ .Tmsá´± Î” Î“ Î´ = to-cwf-tms (to-stlc-tms Î´) â‰¡ Î´

  -- Paths don't compute outside of Cubical Agda so I think we need UIP
  uip : âˆ€ {â„“} {A : Set â„“} {x y : A} {p q : x â‰¡ y} â†’ p â‰¡ q
  uip {p = refl} {q = refl} = refl

  drefl : âˆ€ {â„“} {A : Set â„“} {x} {p : A â‰¡ A} â†’ x â‰¡[ p ]â‰¡ x
  drefl {p = refl} = refl

  -- This implementation of dependent UIP relies type constructor injectivity
  -- I don't know whether injectivity of '_â‰¡_' is provable otherwise...

  -- If we are also given a proof of 'AB : A â‰¡ B' and 'x â‰¡[ AB ]â‰¡ z' then
  -- I think this should be derivable from standard UIP (but I think obligating
  -- callers to provide those extra proofs would be pretty painful)
  duip : âˆ€ {â„“} {A B : Set â„“} {x y : A} {z w : B} {p q} {r : (x â‰¡ y) â‰¡ (z â‰¡ w)}
       â†’ p â‰¡[ r ]â‰¡ q
  duip {p = refl} {q = refl} {r = refl} = refl

  to-cwf-inv-â„‚ : ICwF.Cases to-cwf-inv-ğ•„
  to-cwf-inv-â„‚ .idá´± = to-cwf-tm*âŠ‘ âˆ™ to-cwf-id
  to-cwf-inv-â„‚ ._âˆ˜á´±_ {Ïƒ = Ïƒ} {Î´ = Î´} Ïƒá´± Î´á´± = 
    to-cwf-tms (to-stlc-tms Ïƒ âˆ˜ to-stlc-tms Î´)
    â‰¡âŸ¨ to-cwf-âˆ˜ âŸ©
    to-cwf-tms (to-stlc-tms Ïƒ) ICwF.âˆ˜ to-cwf-tms (to-stlc-tms Î´)
    â‰¡âŸ¨ congâ‚‚ ICwF._âˆ˜_ Ïƒá´± Î´á´± âŸ©
    Ïƒ ICwF.âˆ˜ Î´ âˆ
  to-cwf-inv-â„‚ ._[_]á´± {M = M} {Î´ = Î´} Má´± Î´á´± =
    to-cwf-tm (to-stlc-tm M [ to-stlc-tms Î´ ])
    â‰¡âŸ¨ to-cwf-[] {M = to-stlc-tm M} âŸ©
    to-cwf-tm (to-stlc-tm M) ICwF.[ to-cwf-tms (to-stlc-tms Î´) ]
    â‰¡âŸ¨ congâ‚‚ ICwF._[_] Má´± Î´á´± âŸ©
    M ICwF.[ Î´ ] âˆ
  to-cwf-inv-â„‚ .â€¢á´± = tt
  to-cwf-inv-â„‚ .Îµá´± = refl
  to-cwf-inv-â„‚ ._â–·á´±_ tt tt = tt
  to-cwf-inv-â„‚ ._,á´±_ Î´á´± Má´± = congâ‚‚ ICwF._,_ Î´á´± Má´±
  to-cwf-inv-â„‚ .Ï€â‚€á´± {Î´ = Î´} Î´á´± = 
    to-cwf-tms (to-stlc-tms (ICwF.Ï€â‚€ Î´))
    â‰¡âŸ¨ to-cwf-Ï€â‚€ âŸ©
    ICwF.Ï€â‚€ (to-cwf-tms (to-stlc-tms Î´))
    â‰¡âŸ¨ cong ICwF.Ï€â‚€ Î´á´± âŸ©
    ICwF.Ï€â‚€ Î´ âˆ
  to-cwf-inv-â„‚ .Ï€â‚á´± {Î´ = Î´} Î´á´± = 
    to-cwf-tm (to-stlc-tm (ICwF.Ï€â‚ Î´))
    â‰¡âŸ¨ to-cwf-Ï€â‚ âŸ©
    ICwF.Ï€â‚ (to-cwf-tms (to-stlc-tms Î´))
    â‰¡âŸ¨ cong ICwF.Ï€â‚ Î´á´± âŸ©
    ICwF.Ï€â‚ Î´ âˆ
  to-cwf-inv-â„‚ .oá´± = tt
  to-cwf-inv-â„‚ ._â‡’á´±_ tt tt = tt
  to-cwf-inv-â„‚ ._Â·á´±_ Má´± Ná´± = congâ‚‚ ICwF._Â·_ Má´± Ná´±
  to-cwf-inv-â„‚ .Æ›á´±_ Má´± = cong (ICwF.Æ›_) Má´±

  -- Boring UIP proofs
  to-cwf-inv-â„‚ .idâˆ˜á´±  = duip
  to-cwf-inv-â„‚ .âˆ˜idá´±  = duip
  to-cwf-inv-â„‚ .âˆ˜âˆ˜á´±   = duip
  to-cwf-inv-â„‚ .[id]á´± = duip
  to-cwf-inv-â„‚ .[âˆ˜]á´±  = duip
  to-cwf-inv-â„‚ .â€¢-Î·á´±  = duip
  to-cwf-inv-â„‚ .â–·-Î²â‚€á´± = duip
  to-cwf-inv-â„‚ .â–·-Î²â‚á´± = duip
  to-cwf-inv-â„‚ .â–·-Î·á´±  = duip
  to-cwf-inv-â„‚ .Ï€â‚€âˆ˜á´±  = duip
  to-cwf-inv-â„‚ .Ï€â‚âˆ˜á´±  = duip
  to-cwf-inv-â„‚ .Â·[]á´±  = duip
  to-cwf-inv-â„‚ .Æ›[]á´±  = duip

 
  to-cwf-inv-tm : âˆ€ {M : Î“ ICwF.âŠ¢ A} â†’ to-cwf-tm (to-stlc-tm M) â‰¡ M
  to-cwf-inv-tm {M = M} = elim-tm to-cwf-inv-â„‚ M
  ```
  